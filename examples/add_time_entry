#!perl
use v5.32;
use warnings;

use lib qw(lib);
use experimental qw(signatures);

use Clockify;
use Clockify::DateTime;

use Getopt::Long;

=head1 NAME

add_time_entry - create Clockify time entries

=head1 SYNOPSIS

	# set the environment variable for the project:
	% export CLOCKIFY_PROJECT=EXAMPLE123

	# Add an entry for today with start and end times, and message
	% add_time_entry 1230 1730 "This is what I did"

	# Add an entry for the 20th of this month, with start and end times, and message
	% add_time_entry 20T1230 1730 "This is what I did"

	# Add an entry for January 20, with start and end times, and message
	% add_time_entry 1-20T1230 1730 "This is what I did"

	# If the month is later than this month, it's last year
	% add_time_entry 12-20T1230 1730 "This is what I did"

=head1 DESCRIPTION

=cut

use Mojo::Util qw(dumper);

run( @ARGV ) unless caller;

sub process_args ( $args ) {
	require Getopt::Long;

	my $p = Getopt::Long::Parser->new;

	#if( $p->getoptionsfromarray( $args, ...options descriptions...) ) {}
	}

sub run ( @args ) {
	process_args( \@args );

	my( $start, $end, $description ) = @args;

	my $clockify = Clockify->new;
	say "Active Workspace ID: ", $clockify->user->active_workspace_id;
	say "User ID: ", $clockify->user->id;

	my %query;

	$query{start}       = guess_datetime( $start ),
	$query{end}         = guess_datetime( $end, $query{start} ),
	$query{description} = $description;
	$query{project}     = $clockify->get_project_id;

	show_query( \%query );

	print "Proceed? ";
	my $answer = <STDIN>;
	exit unless $answer =~ /\Ay/i;

	my $result = $clockify->user->add_time_entry( \%query );
	if( $result->is_success ) {
		say <<~"HERE";
		Added entry @{[ $result->id ]}
			@{[ $result->start_date ]}
			@{[ $result->end_date ]}

			@{ [ $result->duration->{elapsed_hours} ] } hours
		HERE
		}
	else {
		say <<~"HERE";
		Error code @{[ $result->code ]}
		@{[ $result->message ]}
		HERE
		}
	}

sub show_query ( $query ) {
	printf <<~"HERE", $query->@{qw(start end project description)};
	-------------------------
	START:       %s
	END:         %s
	PROJECT:     %s
	DESCRIPTION: %s
	-------------------------
	HERE
	}

